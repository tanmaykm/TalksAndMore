{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "legendary-appliance",
   "metadata": {},
   "outputs": [],
   "source": [
    "using AMQPClient\n",
    "using Random"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "maritime-second",
   "metadata": {},
   "source": [
    "Prepare the exchanges and server queues. We shall be using a direct exchange."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "found-fourth",
   "metadata": {},
   "outputs": [],
   "source": [
    "function prepare_queues(chan)\n",
    "    @debug(\"declaring direct exchange named rpcexcg\")\n",
    "    @assert exchange_declare(chan, \"rpcexcg\", EXCHANGE_TYPE_DIRECT)\n",
    "\n",
    "    @debug(\"declaring server queue\")\n",
    "    success, q_name, message_count, consumer_count = queue_declare(chan, \"server\")\n",
    "    @assert success\n",
    "    \n",
    "    @debug(\"binding server queue to receive messages with routing key attribute server\")\n",
    "    @assert queue_bind(chan, \"server\", \"rpcexcg\", \"server\")\n",
    "end\n",
    "\n",
    "function teardown_queues(chan)\n",
    "    @debug(\"unbinding server queue from exchange\")\n",
    "    @assert queue_unbind(chan, \"server\", \"rpcexcg\", \"server\")\n",
    "\n",
    "    @debug(\"deleting server queue\")\n",
    "    success, message_count = queue_delete(chan, \"server\")\n",
    "    @assert success\n",
    "    \n",
    "    @debug(\"deleting exchange\")\n",
    "    @assert exchange_delete(chan, \"rpcexcg\")\n",
    "end;"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "painful-tomorrow",
   "metadata": {},
   "source": [
    "The server process waits on the server queue.\n",
    "It expects a message containing string data, which it reverses and responds back to the route mentioned in `reply_to` attribute of the message. Also attaches the `correlation_id` of the request message back with the response."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "attended-reception",
   "metadata": {},
   "outputs": [],
   "source": [
    "function server_process(chan)\n",
    "    message_count = 0\n",
    "    success, consumer_tag = basic_consume(chan, \"server\", (msg)->begin\n",
    "        datastr = String(copy(msg.data))\n",
    "        @debug(\"server got a message\", datastr)\n",
    "\n",
    "        reply_to = convert(String, msg.properties[:reply_to])\n",
    "        correlation_id = convert(String, msg.properties[:correlation_id])\n",
    "\n",
    "        responsestr = reverse(datastr)\n",
    "        responsedata = convert(Vector{UInt8}, codeunits(responsestr))\n",
    "        responsemsg = Message(responsedata,\n",
    "            content_type=\"text/plain\",\n",
    "            correlation_id=correlation_id,\n",
    "            delivery_mode=PERSISTENT)\n",
    "        @debug(\"server responding\", reply_to, correlation_id, datastr, responsestr)\n",
    "        basic_publish(chan, responsemsg; exchange=\"rpcexcg\", routing_key=reply_to)\n",
    "        \n",
    "        basic_ack(chan, msg.delivery_tag)\n",
    "        message_count += 1\n",
    "    end)\n",
    "    @assert success\n",
    "\n",
    "    while message_count < 10\n",
    "        sleep(1)\n",
    "    end\n",
    "    @debug(\"cancelling server consumer\")\n",
    "    basic_cancel(chan, consumer_tag)\n",
    "end;"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "embedded-wellington",
   "metadata": {},
   "source": [
    "The client process sends a bunch of requests to the server with string data, and when it receives a response, matches the corresponding request to it (based on correlation id) and prints the request and response out."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "authorized-emphasis",
   "metadata": {},
   "outputs": [],
   "source": [
    "function client_process(chan, client_id)\n",
    "    @debug(\"declaring client queue\", client_id)\n",
    "    success, q_name, message_count, consumer_count = queue_declare(chan, client_id)\n",
    "    @assert success\n",
    "    \n",
    "    @debug(\"binding client queue\", client_id)\n",
    "    @assert queue_bind(chan, client_id, \"rpcexcg\", client_id)\n",
    "    \n",
    "    message_count = 0\n",
    "    messages_sent = Dict{Int,String}()\n",
    "    success, consumer_tag = basic_consume(chan, client_id, (msg)->begin\n",
    "        response = String(copy(msg.data))\n",
    "        @debug(\"got response\", client_id, response)\n",
    "        correlation_id = parse(Int, convert(String, msg.properties[:correlation_id]))\n",
    "        request = messages_sent[correlation_id]\n",
    "        @info(\"string reversed\", client_id, request, response)\n",
    "        \n",
    "        basic_ack(chan, msg.delivery_tag)\n",
    "        message_count += 1\n",
    "    end)\n",
    "    @assert success\n",
    "    \n",
    "    for idx in 1:5\n",
    "        datastr = randstring(5)\n",
    "        messages_sent[idx] = datastr\n",
    "        data = convert(Vector{UInt8}, codeunits(datastr))\n",
    "        msg = Message(data,\n",
    "            content_type=\"text/plain\",\n",
    "            correlation_id=string(idx),\n",
    "            reply_to=client_id,\n",
    "            delivery_mode=PERSISTENT)   \n",
    "        @debug(\"requesting string reverse\", client_id, correlation_id=idx, data=datastr)\n",
    "        basic_publish(chan, msg; exchange=\"rpcexcg\", routing_key=\"server\")\n",
    "    end\n",
    "    \n",
    "    while message_count < 5\n",
    "        sleep(1)\n",
    "    end\n",
    "    @debug(\"cancelling $client_id consumer\")\n",
    "    basic_cancel(chan, consumer_tag)\n",
    "\n",
    "    @debug(\"unbinding client queue from exchange\")\n",
    "    @assert queue_unbind(chan, client_id, \"rpcexcg\", client_id)\n",
    "    \n",
    "    @debug(\"deleting client queue\", client_id)\n",
    "    success, message_count = queue_delete(chan, client_id)\n",
    "    @assert success\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "revised-incentive",
   "metadata": {},
   "outputs": [
    {
     "name": "stderr",
     "output_type": "stream",
     "text": [
      "┌ Info: string reversed\n",
      "│   client_id = client1\n",
      "│   request = ool9x\n",
      "│   response = x9loo\n",
      "└ @ Main In[4]:16\n",
      "┌ Info: string reversed\n",
      "│   client_id = client2\n",
      "│   request = 3PGFH\n",
      "│   response = HFGP3\n",
      "└ @ Main In[4]:16\n",
      "┌ Info: string reversed\n",
      "│   client_id = client1\n",
      "│   request = NN3ie\n",
      "│   response = ei3NN\n",
      "└ @ Main In[4]:16\n",
      "┌ Info: string reversed\n",
      "│   client_id = client2\n",
      "│   request = hHdR8\n",
      "│   response = 8RdHh\n",
      "└ @ Main In[4]:16\n",
      "┌ Info: string reversed\n",
      "│   client_id = client1\n",
      "│   request = DIJx7\n",
      "│   response = 7xJID\n",
      "└ @ Main In[4]:16\n",
      "┌ Info: string reversed\n",
      "│   client_id = client2\n",
      "│   request = AYUSv\n",
      "│   response = vSUYA\n",
      "└ @ Main In[4]:16\n",
      "┌ Info: string reversed\n",
      "│   client_id = client1\n",
      "│   request = xjUkU\n",
      "│   response = UkUjx\n",
      "└ @ Main In[4]:16\n",
      "┌ Info: string reversed\n",
      "│   client_id = client2\n",
      "│   request = gapgL\n",
      "│   response = Lgpag\n",
      "└ @ Main In[4]:16\n",
      "┌ Info: string reversed\n",
      "│   client_id = client1\n",
      "│   request = Cexpa\n",
      "│   response = apxeC\n",
      "└ @ Main In[4]:16\n",
      "┌ Info: string reversed\n",
      "│   client_id = client2\n",
      "│   request = Fy76T\n",
      "│   response = T67yF\n",
      "└ @ Main In[4]:16\n",
      "┌ Warning: Discarding message for unknown channel\n",
      "│   channelid = 1\n",
      "└ @ AMQPClient /home/tan/.julia/dev/AMQPClient/src/protocol.jl:398\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "Task (done) @0x00007f172ef6ba90"
      ]
     },
     "execution_count": 5,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "connection(; virtualhost=\"/\",\n",
    "        host=\"localhost\", port=AMQPClient.AMQP_DEFAULT_PORT,\n",
    "        auth_params=AMQPClient.DEFAULT_AUTH_PARAMS) do conn\n",
    "    @sync begin\n",
    "        exchange_prepared = false\n",
    "        clients_done = 0\n",
    "        @async begin\n",
    "            channel(conn, AMQPClient.UNUSED_CHANNEL, true) do chan\n",
    "                prepare_queues(chan)\n",
    "                exchange_prepared = true\n",
    "                server_process(chan)\n",
    "                \n",
    "                while clients_done < 2\n",
    "                    sleep(1)\n",
    "                end\n",
    "                teardown_queues(chan)\n",
    "            end\n",
    "        end\n",
    "        \n",
    "        while !exchange_prepared\n",
    "            sleep(1)\n",
    "        end\n",
    "        \n",
    "        @async begin\n",
    "            channel(conn, AMQPClient.UNUSED_CHANNEL, true) do chan\n",
    "                client_process(chan, \"client1\")\n",
    "            end\n",
    "            clients_done += 1\n",
    "        end\n",
    "        @async begin\n",
    "            channel(conn, AMQPClient.UNUSED_CHANNEL, true) do chan\n",
    "                client_process(chan, \"client2\")\n",
    "            end\n",
    "            clients_done += 1\n",
    "        end\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "documented-survey",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.5.2",
   "language": "julia",
   "name": "julia-1.5"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.5.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
